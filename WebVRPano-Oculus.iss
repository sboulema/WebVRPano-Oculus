; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define ApplicationName "WebVRPano Oculus"
#define SourceDir "bin\Release"
#define ApplicationVersion GetFileVersion("bin\Release\WebVRPano-Oculus.exe")

#include <idp.iss>

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{13D38A4B-A876-49C0-903A-F9E070B56682}}
AppName={#ApplicationName}
AppVersion={#ApplicationVersion}
AppPublisher=Samir L. Boulema
AppPublisherURL=https://github.com/sboulema/WebVRPano-Oculus
AppSupportURL=https://github.com/sboulema/WebVRPano-Oculus
AppUpdatesURL=https://github.com/sboulema/WebVRPano-Oculus
DefaultDirName={pf32}\{#ApplicationName}
DefaultGroupName={#ApplicationName}
AllowNoIcons=yes
OutputBaseFilename={#ApplicationName} {#ApplicationVersion}
;SetupIconFile=PanoStudio\Resources\logo.ico
Compression=lzma
SolidCompression=yes
OutputDir=Installer
UninstallDisplayName={#ApplicationName} {#ApplicationVersion}
;UninstallDisplayIcon=PanoStudio\Resources\logo.ico
AlwaysRestart=yes

[Types]
Name: full;    Description: "Volledige installatie"
Name: compact; Description: "Compacte installatie"
Name: custom;  Description: "Aangepaste installatie"; Flags: iscustom

[Components]
Name: app;  Description: {#ApplicationName}; Types: full compact custom; Flags: fixed
Name: runtime; Description: "Oculus Runtime 0.8.0.0"; Types: full;
Name: chromium; Description: "Chromium WebVR 49.0.2574.0"; Types: full;

[Languages]
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"

[Tasks]
Name: StartAfterInstall; Description: Installeer Oculus Runtime; Components: runtime
Name: StartAfterInstall; Description: Installeer Chromium WebVR; Components: chromium

[Files]
Source: "{#SourceDir}\WebVRPano-Oculus.exe"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{group}\{cm:UninstallProgram,{#ApplicationName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\WebVRPano-Oculus.exe"; Description: "{cm:LaunchProgram,{#ApplicationName}}"; Flags: nowait postinstall skipifsilent
Filename: {tmp}\runtime.exe; Tasks: StartAfterInstall; Parameters: "--mode unattended"; StatusMsg: Installeren Oculus Runtime; Flags: skipifdoesntexist skipifsilent
Filename: {tmp}\chromium.exe; Tasks: StartAfterInstall; Parameters: "-y -o""{app}"""; StatusMsg: Installeren Chrome WebVR; Flags: skipifdoesntexist skipifsilent

[Registry]
Root: HKCR; Subkey: "pano"; ValueType: "string"; ValueData: "URL:Custom Protocol"; Flags: uninsdeletekey
Root: HKCR; Subkey: "pano"; ValueType: "string"; ValueName: "URL Protocol"; ValueData: ""
Root: HKCR; Subkey: "pano\DefaultIcon"; ValueType: "string"; ValueData: "{app}\WebVRPano-Oculus.exe,0"
Root: HKCR; Subkey: "pano\shell\open\command"; ValueType: "string"; ValueData: """{app}\WebVRPano-Oculus.exe"" ""%1"""

[Code]
procedure InitializeWizard;
begin
    idpAddFileComp('http://static.oculus.com/sdk-downloads/0.8.0.0/Public/1445451746/oculus_runtime_sdk_0.8.0.0_win.exe', ExpandConstant('{tmp}\runtime.exe'), 'runtime'); 
    idpAddFileComp('http://software.sboulema.nl/webvrpano/Chromium_WebVR_49.0.2574.0.exe', ExpandConstant('{tmp}\chromium.exe'), 'chromium');

    idpDownloadAfter(wpReady);
end;